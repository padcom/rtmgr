; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "RadioTaxi Manager"
#define MyAppVerName "RadioTaxi Manager 3.7"
#define MyAppPublisher "Aplaline"
#define MyAppURL "http://www.aplaline.com"
#define MyAppExeName "rtmgr.exe"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\RTMgr
DefaultGroupName={#MyAppName}
OutputBaseFilename=rtmgr-setup
Password=hklm709
Encryption=true
Compression=lzma
SolidCompression=true
OutputDir=..\build\bin
LicenseFile=licencja.txt
PrivilegesRequired=admin

[Languages]
Name: polish; MessagesFile: compiler:Languages\Polish.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: installntpc; Description: Instaluj klienta TimeSync; Languages: 
Name: installntpd; Description: Instaluj serwer TimeSync

[Files]
Source: ..\build\bin\rtmgr.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\build\bin\pods.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\build\bin\ntpc.exe; DestDir: {app}; Flags: ignoreversion; Tasks: installntpc
Source: ..\build\bin\ntpd.exe; DestDir: {app}; Flags: ignoreversion; Tasks: installntpd
Source: msvcp71.dll; DestDir: {app}; Flags: ignoreversion
Source: msvcr71.dll; DestDir: {app}; Flags: ignoreversion
Source: gds32.dll; DestDir: {app}; Flags: ignoreversion
Source: GPL.txt; DestDir: {app}; Flags: ignoreversion
Source: licencja.txt; DestDir: {app}; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
Name: {group}\Podsumowanie kursów; Filename: {app}\pods.exe
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: quicklaunchicon

[Run]
Filename: {app}\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent
Filename: {app}\ntpc.exe; Description: {cm:LaunchProgram,Klient TimeSync}; Flags: nowait postinstall; Tasks: installntpc
Filename: {app}\ntpd.exe; Description: {cm:LaunchProgram,Serwer TimeSync}; Flags: nowait postinstall; Tasks: installntpd

[INI]
FileName: {app}\rtmgr.ini; Section: settings; Key: database; String: {code:GetSerwer}:RTMGR
FileName: {app}\pods.ini; Section: settings; Key: database; String: {code:GetSerwer}:RTMGR
FileName: {app}\ntpc.ini; Section: settings; Key: database; String: {code:GetSerwer}; Tasks: installntpc

[Registry]
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: TimeSync client; ValueData: """{app}\nptc.exe"""; Flags: uninsdeletevalue; Tasks: installntpc
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: TimeSync client; ValueData: """{app}\nptd.exe"""; Flags: uninsdeletevalue; Tasks: installntpd

[Code]
var
  Page: TInputQueryWizardPage;

procedure InitializeWizard;
begin
  // Create the page
  Page := CreateInputQueryPage(wpSelectDir,
    'Baza danych', 'Po³¹czenie z baz¹ danych',
    'Proszê podaæ adres serwera bazy danych');

  // Add items (False means it's not a password edit)
  Page.Add('Adres serwera:', False);

  // Set initial values (optional)
  Page.Values[0] := ExpandConstant('127.0.0.1');
end;

function GetSerwer(Param: String): String;
begin
  Result := Page.Values[0];
end;
